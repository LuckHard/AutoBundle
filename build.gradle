// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply from: project.file('metadata.gradle')

final androidSdkPath = System.getenv('ANDROID_HOME') ?: {
    final localProp = new Properties()
    try {
        localProp.load(new FileInputStream("${rootProject.projectDir}/local.properties"))
    } catch (IOException ignore) {}
    localProp['sdk.dir'] ?: {
        throw new RuntimeException("Android sdk not found")
    }()
}()

final m2Repository = "${androidSdkPath}/extras/android/m2repository"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.novoda:bintray-release:0.3.4'
    }
}

ext {
    androidJar = fileTree(dir: "${androidSdkPath}/platforms/android-25/", include:  "android.jar")
    sdkVersion = 25
    buildToolsVersion = '25.0.2'
    supportLibraryVersion = '25.1.0'
}

allprojects {
    repositories {
        maven {
            url m2Repository
        }
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}